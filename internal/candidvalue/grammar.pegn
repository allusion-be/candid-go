# CANDID (v0.1.0) github.com/di-wu/candid-go/internal/candid/candidvalue

Values <-- '(' Sp (Value (Sp ',' Sp Value)*)? Sp ')' / Value
Value   <- Num / Bool / Null / Text

Num      <-- NumValue (Sp ':' Sp NumType)?
NumValue <-- '-'? digit ('_'? digit)* ('.' digit ('_'? digit)*)?
NumType  <-- 'nat8' / 'nat16' / 'nat32' / 'nat64' / 'nat'
           / 'int8' / 'int16' / 'int32' / 'int64' / 'int'
           / 'float32' / 'float64'

Bool       <- BoolValue (Sp ':' Sp 'bool')?
BoolValue <-- ('true' / 'false')

Null <-- 'null'

Text       <-- TextValue (Sp ':' Sp 'text')?
TextValue  <-- '"' Char* '"'

Char     <- Utf / ESC hex{2} / ESC escape / '\u{' HexNum '}'
Utf      <- ascii / UtfEnc
UtfEnc   <- [xC2-xDF] utfcont
          / xE0 [xA0-xBF] utfcont
          / xED [x80-x9F] utfcont
          / [xE1-xEC] utfcont{2}
          / [xEE-xEF] utfcont{2}
          / xF0 [x90-xBF] utfcont{2}
          / xF4 [x80-x8F] utfcont{2}
          / [xF1-xF3] utfcont{3}
utfcont  <- [x80-xBF]

ascii    <- [x20-x21] / [x23-x5B] / [x5D-x7E]
escape   <- 'n' / 'r' / 't'
          / ESC
          / x22 # "
          / x27 # '
HexNum   <- hex   ('_'? hex)*

Sp <- ' '*

digit <- [0-9]
hex    <- digit / [A-F] / [a-f]

ESC <- x5C # \
